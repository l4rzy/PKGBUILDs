# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Patryk Kowalczyk < patryk at kowalczyk dot ws>


_pkgname=libseccomp
pkgname=lib32-${_pkgname}
pkgver=2.3.3
pkgrel=1
pkgdesc='Enhanced seccomp library (32-bit)'
arch=('x86_64')
license=('LGPL2.1')
url="https://github.com/seccomp/libseccomp"
depends=('glibc' 'lib32-glibc')
validpgpkeys=('7100AADFAE6E6E940D2E0AD655E45A5AE8CA7C8A') # Paul Moore <paul@paul-moore.com>
source=("https://github.com/seccomp/$_pkgname/releases/download/v$pkgver/$_pkgname-$pkgver.tar.gz"{,.asc})
sha256sums=('7fc28f4294cc72e61c529bedf97e705c3acf9c479a8f1a3028d4cd2ca9f3b155'
            'SKIP')

build() {
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
    cd $_pkgname-$pkgver
    ./configure --prefix=/usr \
        --libdir=/usr/lib32/ \
        --libexecdir=/usr/lib32 \
        --host=i686-unknown-linux-gnu
    make
}

package() {
    cd $_pkgname-$pkgver
    make DESTDIR="$pkgdir" install
    rm -rf "$pkgdir"/usr/{bin,include,lib,share}/
}
